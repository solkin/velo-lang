ext(int a) abs() int {
    if (a > 0) then a else 0 - a
};

ext(int a) neg() int {
    if (a < 0) then a else 0 - a
};

ext (int num) format(int radix) str {
    int MIN_RADIX = 2;
    int MAX_RADIX = 36;
    array[byte] digits = new array[byte]{
        '0' , '1' , '2' , '3' , '4' , '5' ,
        '6' , '7' , '8' , '9' , 'a' , 'b' ,
        'c' , 'd' , 'e' , 'f' , 'g' , 'h' ,
        'i' , 'j' , 'k' , 'l' , 'm' , 'n' ,
        'o' , 'p' , 'q' , 'r' , 's' , 't' ,
        'u' , 'v' , 'w' , 'x' , 'y' , 'z'
    };
    if (radix < MIN_RADIX | radix > MAX_RADIX) then radix = 10;

    # For negative numbers, print out the absolute value w/ a leading '-'.
    # Use an array large enough for a binary number.
    array[byte] buffer = new array[byte](33);
    int i = 33;
    bool isNeg = false;
    if (num < 0) {
        isNeg = true;
        num = 0 - num;

        # When the value is MIN_VALUE, it overflows when made positive
        if (num < 0) {
            i = i - 1;
            buffer[i] = digits[0 - (num + radix) % radix];
            num = 0 - (num / radix);
        };
    };

    while (num > 0) {
        i = i - 1;
        buffer[i] = digits[num % radix];
        num = num / radix;
    };

    if (isNeg) {
        i = i - 1;
        buffer[i] = '-';
    };

    # Package constructor avoids an array copy.
    #new String(buffer, i, 33 - i, true);
    int n = i;
    str result = "";
    while (n < 33) {
        result = result.con(buffer[n].char);
        n = n + 1;
    };
    result;
};
