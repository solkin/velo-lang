func calc(slice[slice[int]] matrix, int x, int y) int {
     func window(int a) int {
        if (a >= 0) {
            if (a < matrix.len) then a else a - matrix.len;
        } else {
            matrix.len + a;
        }
    };

    auto cells =
            matrix[window(y - 1)][window(x - 1)] +
            matrix[window(y - 1)][x] +
            matrix[window(y - 1)][window(x + 1)] +
            matrix[y][window(x - 1)] +
            matrix[y][window(x + 1)] +
            matrix[window(y + 1)][window(x - 1)] +
            matrix[window(y + 1)][x] +
            matrix[window(y + 1)][window(x + 1)];

    if cells == 2 then {
        matrix[y][x]
    } else if cells == 3 then {
        1
    } else {
        0
    }
};

func printWorld(slice[slice[int]] world) slice[slice[int]] {
    world.map(
        func(int index, slice[int] row) slice[int] {
            row.map(
                func(int index, int col) int {
                    print(col);
                    print(" ");
                    col;
                }
            );
            println("");
            row
        }
    )
};

func nextGeneration(slice[slice[int]] world) slice[slice[int]] {
    world.map(
        func(int y, slice[int] row) slice[int] {
            row.map(
                func(int x, int col) int {
                    calc(world, x, y);
                }
            );
        }
    );
};

auto w = sliceOf[slice[int]](
    sliceOf[int](0, 1, 0, 0, 0, 0),
    sliceOf[int](0, 0, 1, 0, 0, 0),
    sliceOf[int](1, 1, 1, 0, 0, 0),
    sliceOf[int](0, 0, 0, 0, 0, 0),
    sliceOf[int](0, 0, 0, 0, 0, 0),
    sliceOf[int](0, 0, 0, 0, 0, 0)
);

print("Enter generations count: ");
int count = 5; # TODO: readLine();
println("");
int idx = 0;
while (idx <= count) {
    printWorld(w);
    println("--");
    w = nextGeneration(w);
    idx = idx + 1;
};
